apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

group = "com.mikolaj.nowak"
archivesBaseName = "properties_names_generator"
version = "1.0.0"

jar {
    manifest {
        attributes 'Implementation-Title': 'Propertie names generator - processor',
                'Implementation-Version': version
    }
}

dependencies {
    implementation project(":annotation")
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                name 'properties-names-generator'
                // optionally artifactId can be defined here
                description 'Library for generating class for data models with properties names assigned as strings, so they can be easily used in sql, or other queries which uses properties names.'
                url 'https://github.com/mikikrk/properties-names-generator'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mikikrk'
                        name 'Miko≈Çaj Nowak'
                        email 'mn.mikolaj.nowak@gmail.com'
                    }
                }
            }
        }
    }
}
